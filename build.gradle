plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id 'me.fallenbreath.yamlang' version '1.3.1'
	id "me.modmuss50.mod-publish-plugin" version "0.4.4"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	mavenLocal()

	maven {
		url "https://maven.enjarai.nl/releases"
	}
	maven {
		url "https://maven.enjarai.nl/mirrors"
	}

	// Cardinal Components API
//	maven {
//		name = "Ladysnake Mods"
//		url = 'https://ladysnake.jfrog.io/artifactory/mods'
//	}

	// YACL
	maven { url = "https://maven.isxander.dev/releases" }

	// Cloth Config.
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }

	// Permissions API, which is a dep of DABR
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	modImplementation("nl.enjarai:do-a-barrel-roll:${project.do_a_barrel_roll_version}") {
		exclude group: "dev.isxander"
	}

	modImplementation("nl.enjarai:cicada-lib:${project.cicada_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	modImplementation("dev.isxander:yet-another-config-lib:${project.yacl_version}-fabric") {
		exclude group: "net.fabricmc"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

yamlang {
	targetSourceSets = [sourceSets.main]
	inputDir = "assets/rolling_down_in_the_deep/lang"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

publishMods {
	file = remapJar.archiveFile
	displayName = "${property('mod_version')} for ${property('minecraft_version')}"
	version = project.version
	changelog = getRootProject().file("CHANGELOG.md").text
	type = BETA
	modLoaders.add("fabric")

	def min = property('publish_target_min')
	def max = property('publish_target_max')

	if (providers.gradleProperty('enjaraiModrinthToken').present) {
		modrinth {
			projectId = property('mod_modrinth')
			accessToken = providers.gradleProperty('enjaraiModrinthToken').get()

			if (min == max) {
				minecraftVersions.add(min)
			} else {
				minecraftVersionRange {
					start = min
					end = max
				}
			}

			requires {
				slug = "fabric-api"
			}
			requires {
				slug = "cicada"
			}
			requires {
				slug = "do-a-barrel-roll"
			}
		}
	}

	if (providers.gradleProperty('enjaraiCurseforgeToken').present) {
		curseforge {
			projectId = property('mod_curseforge')
			accessToken = providers.gradleProperty('enjaraiCurseforgeToken').get()

			if (min == max) {
				minecraftVersions.add(min)
			} else {
				minecraftVersionRange {
					start = min
					end = max
				}
			}

			requires {
				slug = "fabric-api"
			}
			requires {
				slug = "cicada"
			}
			requires {
				slug = "do-a-barrel-roll"
			}
		}
	}

	if (providers.gradleProperty('enjaraiGithubToken').present) {
		github {
			repository = property('mod_github')
			accessToken = providers.gradleProperty('enjaraiGithubToken').get()

			commitish = property('git_branch')
			tagName = project.version
		}
	}
}